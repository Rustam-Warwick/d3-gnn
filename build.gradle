plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

group = 'org.apache.flink.gnn'
version = '1.0'
mainClassName = 'helpers.Main'
description = """GNN Plugin for apache Flink"""

ext {
    javaVersion = '1.8'
    flinkVersion = '1.16.0'
    scalaBinaryVersion = '_2.12'
    slf4jVersion = '1.7.32'
    log4jVersion = '2.17.1'
}

sourceCompatibility = "11"
targetCompatibility = "11"

applicationDefaultJvmArgs = ["-ea","-Dlog4j.configurationFile=log4j2.properties", "-Dfile.encoding=ASCII"]

// declare where to find the dependencies of your project
repositories {
    mavenCentral()
    maven {
        url "https://repository.apache.org/content/repositories/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}


tasks.getByName("compileJava"){
    options.encoding = "UTF-8"
}

sourceSets{
    lib{
        java{

        }
    }
    main{
        java{
            compileClasspath = sourceSets.lib.output + compileClasspath
            runtimeClasspath = sourceSets.lib.output + runtimeClasspath
        }
    }
}

configurations{
    libImplementation.extendsFrom(libApi)
    implementation.extendsFrom(libImplementation)
    runtimeOnly.extendsFrom(libRuntimeOnly)
}

dependencies{

    libImplementation 'org.jetbrains:annotations:23.0.0'
    libImplementation "org.apache.flink:flink-clients:${flinkVersion}"

    implementation "org.apache.flink:flink-statebackend-rocksdb:${flinkVersion}"
    implementation "org.apache.flink:flink-connector-files:${flinkVersion}"

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"

    annotationProcessor 'info.picocli:picocli-codegen:4.7.0'

    libApi 'info.picocli:picocli:4.7.0'
    libApi("org.apache.flink:statefun-flink-core:3.2.0"){transitive(false)}
    libApi('org.apache.flink:flink-ml-iteration_2.12:2.0.0'){transitive(false)}
    libApi 'ai.djl:api:0.19.0'
    libApi 'ai.djl.pytorch:pytorch-engine:0.19.0'
    libApi 'com.boundary:high-scale-lib:1.0.6'
    libApi 'com.github.ben-manes.caffeine:caffeine:3.1.1'
    libApi 'com.esotericsoftware:reflectasm:1.11.9'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"] // Using annotation processor for CMD args Parser
}

task libShadowJar(type: ShadowJar){
    // Other Shadow Task that prepares all the library dependencies
    configurations =  [project.configurations.libApi]
    from sourceSets.lib.output
    dependencies {
        exclude(dependency('org.apache.flink:force-shading'))
        exclude(dependency('com.google.code.findbugs:jsr305'))
        exclude(dependency('org.slf4j:'))
        exclude(dependency('org.apache.logging.log4j:'))
    }
    archiveAppendix.set('lib')
    mergeServiceFiles()
}

shadowJar{
    configurations = []
    archiveClassifier.set("")
}

task bothJars{
    dependsOn(libShadowJar, shadowJar)
}